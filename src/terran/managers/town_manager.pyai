build_finish(1, Command Center)
build_finish(1, SCV)

include(freemoney)

multirun_loop:
    defenseclear()
    if owned(Barracks):
        if enemyowns(hatchery):
            if notowns(Armory):
                defense_ground(Marine)
                defense_air(Marine)
        if notowns(factory):
            defense_ground(Marine)
            defense_air(Marine)            
    if owned(Factory):
        if owned(Machine Shop):
            defense_ground(Siege Tank)
        defense_ground(Vulture)
        if owned(Armory):
            defense_air(Goliath)
    if owned(Starport):
        if enemyowns(Nexus):
            defense_air(Wraith)
        if enemyowns(Command Center):
            defense_air(Wraith)
        if enemyowns(Hatchery):
            if owned(Armory):
                defense_air(Valkyrie)
    if enemyowns(Hatchery):
        if owned(Science Facility):
            defense_ground(Science Vessel)
            defense_air(Science Vessel)
        if owned(Physics Lab):
            defense_ground(Battlecruiser)
            defense_air(Battlecruiser)

multirun_loop:
    if enemyownscloaked():
        player_need(1, Barracks)
        player_need(1, Engineering Bay)
        player_need(1, Academy)
        build_start(1, missile turret, 50)
        build_start(1, Comsat Station, 50)
        wait(300)
        if enemyowns(hatchery):
            if owned(armory):
                # We're going mech in TvZ, so make more turrets
                build_start(2, Missile Turret)
                build_start(3, Missile Turret)
                build_start(4, Missile Turret)
        wait_until(18)
        build_start(2, Missile Turret)
        build_start(3, Missile Turret)
        build_start(4, Missile Turret)
        if enemyowns(hatchery):
            if owned(armory):
                # We're going mech in TvZ, so make more turrets
                build_start(5, Missile Turret)
                build_start(6, Missile Turret)
        stop()
    if enemyownsairtech():
        player_need(1, Engineering Bay)
        build_start(1, missile turret, 50)
        build_start(2, missile turret, 50)
        if enemyowns(hatchery):
            if owned(armory):
                # We're going mech in TvZ, so make more turrets
                build_start(3, Missile Turret)
                build_start(4, Missile Turret)
        wait(300)
        player_need(1, Academy)
        build_start(1, Comsat Station, 50)
        wait_until(18)
        build_start(3, Missile Turret)
        build_start(4, Missile Turret)
        if enemyowns(hatchery):
            if owned(armory):
                # We're going mech in TvZ, so make more turrets
                build_start(5, Missile Turret)
                build_start(6, Missile Turret)
        stop()

    ## lazy hack to enable comsat and turrets build vs protoss (human) ##
    if enemyowns(Citadel of Adun):
        player_need(1, Engineering Bay)
        build_start(1, missile turret, 50)
        build_start(2, missile turret, 50)
        wait(300)
        player_need(1, Academy)
        build_start(1, Comsat Station, 50)
        wait_until(18)
        build_start(3, Missile Turret)
        build_start(4, Missile Turret)
        stop()

loop:
    wait(300)
    if owned(machine shop):
        build_finish(1, Factory)
        build(1, Machine Shop, 80)
        wait_until(21)
        build_finish(2, Factory, 10)
        build(2, Machine Shop, 10)
        stop()
    if time(10):
        if enemyowns(hatchery):
            build_start(1, barracks)
            if resources(500,0):
                build_start(2, barracks, 10)
            wait(3000)
            build_start(2, barracks, 10)
